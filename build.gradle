buildscript {
    ext.artifact = [
        groupId : 'com.github.sasachichito',
        majorVersion : '0',
        minorVersion : '1',
        patchVersion : '0',
        compatible : '1.8'
    ]
    ext.versions = [
        bintrayPlugin : '1.8.4',
        junitPlatformPlugin : '1.0.1',
        junitJupiterVersion : '5.0.1',
        apiguardian : '1.0.0',
        mockitoCore : 'latest.release'
    ]
    ext.bintrayConf = [
        repositoryName : 'maven',
        packageName : 'commons-domain',
        userName : project.hasProperty("bintrayUser") ? bintrayUser : "",
        apiKey : project.hasProperty("bintrayKey") ? bintrayKey : "",
        publishNeeds : true
    ]
    ext.publishConf = [
        moduleName : 'aModule'
    ]
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${versions.bintrayPlugin}"
        classpath "org.junit.platform:junit-platform-gradle-plugin:${versions.junitPlatformPlugin}"
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'maven-publish'

group artifact.groupId
version "${artifact.majorVersion}.${artifact.minorVersion}.${artifact.patchVersion}"
sourceCompatibility = artifact.compatible

repositories {
    mavenCentral()
}

dependencies {
    testCompile "org.junit.jupiter:junit-jupiter-api:${versions.junitJupiterVersion}"
    testCompile "org.junit.jupiter:junit-jupiter-params:${versions.junitJupiterVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${versions.junitJupiterVersion}"
    testCompileOnly "org.apiguardian:apiguardian-api:${versions.apiguardian}"
    testCompile "org.mockito:mockito-core:${versions.mockitoCore}"
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task javaDocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        "${publishConf.moduleName}"(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javaDocJar
        }
    }
}

bintray {
    user = bintrayConf.userName
    key = bintrayConf.apiKey
    publications = [publishConf.moduleName]
    publish = bintrayConf.publishNeeds
    pkg {
        repo = bintrayConf.repositoryName
        name = bintrayConf.packageName
    }
}